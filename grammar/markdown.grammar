%option case-insensitive
%option reentrant

/* 
match unicode characters as one rule instead of each byte.
http://stackoverflow.com/questions/10252777/making-lex-to-read-utf-8-doesnt-work?lq=1
 */
u2a     [\xC2-\xDF][\x80-\xBF]
u2b     \xE0[\xA0-\xBF][\x80-\xBF]
u3a     [\xE1-\xEC\xEE\xEF][\x80-\xBF]{2}
u3b     \xED[\x80-\x9F][\x80-\xBF]
u4a     \xF0[\x90-\xBF][\x80-\xBF]{2}
u4b     [\xF1-\xF3][\x80-\xBF]{3}
u4c     \xF4[\x80-\x8F][\x80-\xBF]{2}
utf_8   {u2a}|{u2b}|{u3a}|{u3b}|{u4a}|{u4b}|{u4c}

h              [0-9a-f]
nonascii       [\200-\377]
unicode        \\{h}{1,6}[ \t\r\n\f]?
escape         {unicode}|\\[ -~\200-\377]
nmchar         [a-z0-9-]|{nonascii}|{escape}
em1            \*[^(\*|\n)]+\*
strong1        \*\*[^(\*|\n)]*\*\*
strongem1      \*\*\*[^(\*|\n)]*\*\*\*
em2            _[^(_|\n)]*_
strong2        __[^(_|\n)]*__
strongem2      ___[^(_|\n)]*___
header1        ^#{1,6}.*
header2        .+\n[=-]+\n
bulletstart    ^[-\+*]+[ ]
blockquote     ^>+
codeline       [ ]
hr             ((-+[ ]{0,2}){3,}|.(_+[ ]{0,2}){3,}|(\*+[ ]{0,2}){3,})\n[ \t]*  

protocol       (http|ftp)s?\:\/\/
subdomain      {nmchar}\.
urlpath        [\/]|{nmchar}+
protourl       {protocol}[^ \n]+
optprotourl    {protocol}?{subdomain}*{nmchar}+\.{nmchar}+{urlpath}*?
href           \[.+\]\(.+\)

em             {em1}|{em2}
strong         {strong1}|{strong2}
strongem       {strongem1}|{strongem2}
strikethrough  ~~.*?~~

url            {protourl}|{optprotourl}

w              [ \t\r\n\f]*
nl             \n|\r\n|\r|\f

%%

{hr}                            {markdownConsume(yytext, MARKDOWNHR, yyscanner);}
{em}                            {markdownConsume(yytext, MARKDOWNEM, yyscanner);}
{strong}                        {markdownConsume(yytext, MARKDOWNSTRONG, yyscanner);}
{strongem}                      {markdownConsume(yytext, MARKDOWNSTRONGEM, yyscanner);}
{strikethrough}                 {markdownConsume(yytext, MARKDOWNSTRIKETHROUGH, yyscanner);}
{header1}                       {markdownConsume(yytext, MARKDOWNHEADER, yyscanner);}
{header2}                       {markdownConsume(yytext, MARKDOWNMULTILINEHEADER, yyscanner);}
{url}                           {markdownConsume(yytext, MARKDOWNURL, yyscanner);}
{href}                          {markdownConsume(yytext, MARKDOWNHREF, yyscanner);}
{bulletstart}                   {markdownConsume(yytext, MARKDOWNBULLETSTART, yyscanner);}
{blockquote}                    {markdownConsume(yytext, MARKDOWNBLOCKQUOTE, yyscanner);}

[\n]{1}                         {markdownConsume(yytext, MARKDOWNNEWLINE, yyscanner);}
[\n]{2,}                        {markdownConsume(yytext, MARKDOWNPARAGRAPH, yyscanner);}
[ \n\t\f]                       {markdownConsume(yytext, MARKDOWNUNKNOWN, yyscanner);}
{utf_8}                         {markdownConsume(yytext, MARKDOWNUNKNOWN, yyscanner);}
.                               {markdownConsume(yytext, MARKDOWNUNKNOWN, yyscanner);}

%%

int markdownwrap(yyscan_t yyscanner){return 1;}
